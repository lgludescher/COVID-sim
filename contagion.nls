to spread-contagion
  if propagation-by-coordinates-proximity?[spread-by-coordinates-proximity]
  
  if activity-based-progagation?
  [spread-by-activity-proximity]
end

to spread-by-coordinates-proximity
  if propagation-by-coordinates-proximity?
  [
    ask people with [is-contagious?]
    [ask people in-radius propagation-radius with [not is-immune?]
      [
        apply-risk-of-contamination 1
      ]
    ]
  ]
end

to spread-by-activity-proximity
  ask people [
    if is-away? [apply-risk-of-contamination-when-away]
    if travelling-propagation? [apply-risk-of-contamination-when-travelling]
    
    let number-contamination count (other [link-neighbors] of current-activity) with [current-activity = [current-activity] of myself and is-contagious?]
    repeat number-contamination [ apply-risk-of-contamination [proximity-factor] of current-activity]
  ]
end

to apply-risk-of-contamination-when-travelling
  let travellers people with [is-moving-outside-for-reaching-current-activity?]
  
  ;;could be fastened with a bit of math, like 1-(1-p)^k
  repeat (count travellers with [is-contagious?]) [
    apply-risk-of-contamination density-travelling-propagation]
end

to apply-risk-of-contamination-when-away
  if random-float 1 < probability-infection-when-abroad and infection-status = "healthy"
  [
    set-contaminated
  ]
end

to set-contaminated
  if disease-model = "markovian" 
    [set infection-status "infected"]
  if disease-model = "advanced"
  [
    set infection-status "asymptomatic noncontagious" 
    set duration-current-disease-status infection-to-asymptomatic-contagiousness
  ]
end

to apply-risk-of-contamination [factor]
  let contagion-occurs? false
  if (not is-away? and random-float 1 < factor * propagation-risk-yom and infection-status = "healthy")
  [set contagion-occurs? true]
  
  if contagion-occurs?
  [
    set-contaminated
  ]
end

to-report is-contagious?
  report not (infection-status = "healthy") and not (infection-status = "immune")
end

to-report is-alive?
  report not (infection-status = "dead")
end

